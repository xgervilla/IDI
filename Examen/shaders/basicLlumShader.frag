#version 330 core

out vec4 FragColor;

uniform vec3 colFocus;
uniform vec3 posFocus;

in vec3 matambFrag;
in vec3 matdiffFrag;
in vec3 matspecFrag;
in float matshinFrag;

in vec3 vertexFrag;
in vec3 posFocFrag;
in vec3 matNormFrag;

vec3 llumAmbient = vec3(0.2,0.2,0.2);

vec3 Lambert (vec3 NormSCO, vec3 L) {
    // S'assumeix que els vectors que es reben com a parÃ metres estan normalitzats

    // Inicialitzem color a component ambient
    vec3 colRes = llumAmbient * matambFrag;

    // Afegim component difusa, si n'hi ha
    if (dot (L, NormSCO) > 0)
      colRes = colRes + colFocus * matdiffFrag * dot (L, NormSCO);
    return (colRes);
}

vec3 Phong (vec3 NormSCO, vec3 L, vec4 vertSCO) {
    // Els vectors estan normalitzats

    // Inicialitzem color a Lambert
    vec3 colRes = Lambert (NormSCO, L);

    // Calculem R i V
    if (dot(NormSCO,L) < 0)
      return colRes;  // no hi ha component especular

    vec3 R = reflect(-L, NormSCO); // equival a: normalize (2.0*dot(NormSCO,L)*NormSCO - L);
    vec3 V = normalize(-vertSCO.xyz);

    if ((dot(R, V) < 0) || (matshinFrag == 0))
      return colRes;  // no hi ha component especular
    
    // Afegim la component especular
    float shine = pow(max(0.0, dot(R, V)), matshinFrag);
    return (colRes + matspecFrag * colFocus * shine); 
}

void main(){
	
	vec3 llum = normalize(posFocFrag - vertexFrag);
	vec3 matNormal = normalize(matNormFrag);
	
    vec3 fcolor = Phong(matNormal, llum, vec4(vertexFrag,1.));
    
	FragColor = vec4(fcolor,1);	
}

